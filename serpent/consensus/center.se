# This software (Augur) allows buying and selling event options in Ethereum.
#
# Copyright (c) 2015 Forecast Foundation
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
# If you have questions, please contact Jack Peterson (jack@augur.net).

def center(reports_filled:arr, reputation:arr, scaled:arr, scaled_max:arr, scaled_min:arr, max_iterations, max_components):
    with num_reports = len(reputation):
        with flatsize = len(reports_filled):
            with num_events = flatsize / num_reports:
                # calculate weighted centered data matrix
                with weighted_means = array(num_events):
                    with total_weight = 0:
                        with i = 0:
                            while i < num_reports:
                                with j = 0:
                                    while j < num_events:
                                        weighted_means[j] += reputation[i] * reports_filled[i * num_events + j]
                                        j += 1
                                total_weight += reputation[i]
                                i += 1
                            with j = 0:
                                while j < num_events:
                                    weighted_means[j] /= total_weight
                                    j += 1
                            # weighted centered data (wcd): [0, flatsize-1]
                            # loading_vector [1 0 0 . . . 0]: [flatsize, flatsize + num_events]
                            with lflatsize = flatsize + num_events + 2:
                                with wcd = array(lflatsize):
                                    with i = 0:
                                        while i < flatsize:
                                            wcd[i] = reports_filled[i] - weighted_means[i % num_events]
                                            i += 1
                                        wcd[i] = ONE
                                        wcd[lflatsize - 2] = max_iterations
                                        if max_components < num_events:
                                            wcd[lflatsize - 1] = max_components
                                        else:
                                            wcd[lflatsize - 1] = num_events
                                        return(wcd: arr)

def tokenize(reputation:arr, num_reports):
    with tokens = array(num_reports):
        with i = 0:
            while i < num_reports:
                tokens[i] = reputation[i] * COINS
                i += 1
            return(tokens: arr)

# First row of the covariance matrix
def covariance(wcd:arr, tokens:arr, num_reports, num_events):
    with cov = array(num_events):
        with wcd_x_tokens = array(num_reports):
            with i = 0:
                while i < num_reports:
                    wcd_x_tokens[i] = fixed_multiply(wcd[i*num_events], tokens[i])
                    i += 1
            with alltokens = sum(tokens):
                with i = 0:
                    while i < num_events:
                        with j = 0:
                            while j < num_reports:
                                cov[i] += fixed_multiply(wcd_x_tokens[j], wcd[j*num_events + i])
                                j += 1
                        cov[i] = fixed_divide(cov[i], alltokens - ONE)
                        i += 1
                    return(cov: arr)

inset('../macros/constants.se')
inset('../macros/fixed.se')
inset('../macros/arrays.se')
